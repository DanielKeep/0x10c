
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; Constants.
                    ;

.include "../../includes/constants.i"
.include "../../includes/lem1802.i"

.include "panic.i"

.equ SCR_FONT_BASE  0x8180
.equ GLYPH_00L      0x417f
.equ GLYPH_00H      0x7f0c
.equ GLYPH_01L      0x0c7f
.equ GLYPH_01H      0x7f41

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; Code.
                    ;

.org 0x0000
                    set pc, __entry

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; do_syscall
                    ;
                    ; Invokes a syscall.  The calling convention is as follows:
                    ;
                    ; 1.    Prepare the registers and stack as you would if
                    ;       directly calling the syscall function.
                    ; 2.    Store the syscall number into the Z register.
                    ; 3.    jsr do_syscall.
                    ;

.org 0x0010
:do_syscall         ; Make sure the syscall is valid.
                    ifg z, [syscall_table_max]
                        set pc, .bad_syscall

                    ; Turn A into the syscall address.
                    add z, syscall_table
                    set z, [z]

                    ; Finally, we can call the syscall itself.
                    set pc, z

:.bad_syscall       set a, PANIC_BAD_SYSCALL
                    set pc, sys_panic_because

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; __entry() -> DNR
                    ;
                    ; Entry point into the OS.
                    ;

:__entry            ; Zero out 0 so jumps to it fail.
                    set [0], 0
                    set [1], 0

                    ; Compute syscall table size.
                    set a, syscall_table_end
                    sub a, syscall_table
                    set [syscall_table_len], a
                    sub a, 1
                    set [syscall_table_max], a

                    ; Start initialising stuff.
                    set a, pick 0
                    jsr mem_init

                    set a, pick 2
                    set b, pick 3
                    set c, pick 4
                    jsr scr_init

                    set a, pick 1
                    jsr disk_init

                    set [kb_id], pick 5

                    ; Clear stack.
                    set sp, 0

                    ; Print hello, world message.
                    jsr sys_hello_world
                    jsr sys_panic

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; sys_panic() -> DNR
                    ;

:sys_panic          set ex, -1
                    hwi [debug_id]
                    sub pc, 1

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; sys_panic_because(A = reason) -> DNR
                    ;

:sys_panic_because  set [sys_panic_reason], a
                    set pc, sys_panic

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; sys_panic_because_1(A = reason, B = arg0) -> DNR
                    ;

:sys_panic_because_1
                    set [sys_panic_reason], a
                    set [sys_panic_arg0], b
                    set pc, sys_panic

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; sys_panic_because_2(A = reason, B = arg0, C = arg1) -> DNR
                    ;

:sys_panic_because_2
                    set [sys_panic_reason], a
                    set [sys_panic_arg0], b
                    set [sys_panic_arg1], c
                    set pc, sys_panic

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; sys_panic_zero_syscall() -> DNR
                    ;

:sys_panic_zero_syscall
                    set [sys_panic_reason], PANIC_ZERO_SYSCALL
                    set pc, sys_panic

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; sys_hello_world() -> DNR
                    ;

:sys_hello_world    set a, .HELLO
                    set [scr_format], 0xa000
                    jsr scr_putpsz

                    set a, LEM1802_MEM_DUMP_FONT
                    set b, SCR_FONT_BASE
                    hwi [scr_id]

                    set [SCR_FONT_BASE+0], GLYPH_00L
                    set [SCR_FONT_BASE+1], GLYPH_00H
                    set [SCR_FONT_BASE+2], GLYPH_01L
                    set [SCR_FONT_BASE+3], GLYPH_01H

                    set a, LEM1802_MEM_MAP_FONT
                    set b, SCR_FONT_BASE
                    hwi [scr_id]

                    set [scr_format], 0xc000
                    set a, .MOTTO_1
                    jsr scr_putpsz
                    set a, 0
                    jsr scr_putcr
                    set a, 1
                    jsr scr_putcr
                    set a, .MOTTO_2
                    jsr scr_putpsz

                    sub pc, 1

                    ;     12345678901234567890123456789012
:.HELLO             dat p"Hello, World!\n\0"

:.MOTTO_1           dat p"If it ain't a \0"
:.MOTTO_2           dat p"it, it's a...\n\0"

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; Variables.
                    ;

:sys_panic_reason   dat 0
:sys_panic_arg0     dat 0
:sys_panic_arg1     dat 0

:dd_id              dat 0xffff
:kb_id              dat 0xffff

:debug_id           dat 0x0001 ; Hard-coded for now.

                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    ;
                    ; Static data.
                    ;
